  $MOD51

  	U EQU 31        ;MEMORY LOCATION TO HOLD UPPER NIBBLE
  	L EQU 32 	;MEMORY LOCATION TO HOLD LOWER NIBBLE

	PORT 	EQU 	P1      ;DATA PORT TO CONNECT LCD
	RS 	EQU 	P3.4    ;RS PIN CONNECTION
  	EN 	EQU 	P3.6    ;EN PIN CONNECTION
  	
  	SEL EQU 41H

  	ORG 	0000H
  	ACALL 	INIT
	MOV DPTR,#TEXT1
	ACALL 	LCD_OUT     ;IN FIRST LINE OF LCD


  	MOV A, #0C0H         ;SWITCH TO 2ND LINE OF LCD
  	ACALL 	LCD_CMD  

	MOV DPTR,#TEXT2
	ACALL 	LCD_OUT     ;IN FIRST LINE OF LCD
  	SJMP 	$     ;INFINITE LONG LOOP

INIT:
   	ACALL 	DELAY     	;SOME DELAY TO LCD AFTER POWER ON
   	ACALL 	DELAY

   	MOV 	PORT, #20H  	;SEND 20H TO LCD TO SET 4 BIT MODE
   	CLR 	RS   		;AFTER THAT WE CAN USE LCD_CMD
   	SETB 	EN         	;MAKE EN SWITCHING
   	ACALL 	DELAY
   	CLR 	EN

	ACALL	INIT_IN

   	RET


INIT_IN:
	MOV 	DPTR,#INIT_COMMANDS
 	SETB 	SEL
  	ACALL 	LCD_OUT
   	CLR 	SEL
    	RET


LCD_OUT:  CLR A
           MOVC A,@A+DPTR
           JZ EXIT
           INC DPTR
           JB SEL,CMD
           ACALL LCD_DATA
           SJMP LCD_OUT
CMD:      ACALL LCD_CMD
           SJMP LCD_OUT
EXIT:	   RET


LINE2:MOV A,#0C0H 
    ACALL LCD_CMD
    RET   
    
LINE1: MOV A,#80H    
ACALL LCD_CMD
RET

 
LCD_CMD:
    CLR 	RS     			;CLEAR RS, GOING TO SEND COMMAND
    ACALL 	SEPARATOR     	;SEPARATE THE COMMAND AND SAVE TO U AND L
    MOV 	A, U     		;COPY U TO A
    ACALL 	MOVE_TO_PORT  	;MOVE CONTENT OF A TO PORT 
    MOV 	A, L            ;COPY L TO A
    ACALL 	MOVE_TO_PORT  	;MOVE CONTENT OF A TO PORT
    RET       				;RETURN
 
LCD_DATA:
   SETB RS            		;RS=1, GOING TO SEND DATA
   ACALL SEPARATOR    		;SEPARATE THE DATA AND SAVE TO U & L
   MOV A, U           		;COPY U TO A
   ACALL MOVE_TO_PORT 		;SEND IT TO LCD
   MOV A, L           		;COPY L TO A
   ACALL MOVE_TO_PORT 		;SEND IT TO LCD
   RET                		;RETURN

SEPARATOR:
   MOV U,A        			;SAVE A AT TEMP LOCATION U
   ANL U,#0F0H    			;MASK IT  WITH 0FH (28H & F0H = 20H) 
   SWAP A         			;SWAP NIBBLE (28H => 82H)
   ANL A,#0F0H    			;MASK IT WITH 0FH (82H & F0H = 80H)
   MOV L,A        ;SAVE IT AT TEMP LOCATION L
   RET            ;RETURN


MOVE_TO_PORT:
   	MOV 	PORT,A       ;PUT CONTENT OF A TO PORT
   	SETB 	EN    	;MAKE EN HIGH
   	ACALL 	DELAY   ;CALL A SHORT DELAY ROUTINE
   	CLR 	EN     ;CLEAR EN
   	ACALL 	DELAY   ;SHORT DELAY
   	RET     	;RETURN

DELAY:
   	MOV 	R0, #20H
L2: MOV 	R1, #0FH
L1: DJNZ 	R1, L1
   	DJNZ 	R0, L2
   	RET

INIT_COMMANDS:  DB 28H,0CH,06H,01H,0
TEXT1: DB "Exp: 04 (a)",0 
TEXT2: DB "4 BIT LCD",0 
	END